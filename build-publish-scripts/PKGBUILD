# Maintainer: pierspad <pierpaolospadafora@proton.me>
pkgname=textmerger
pkgver=2.2.2
pkgrel=1
pkgdesc="A Rust/Tauri GTK3 application for merging text files"
arch=('x86_64')
url="https://github.com/pierspad/textmerger"
license=('GPL3')

depends=(
  'webkit2gtk-4.1' 
  'gtk3' 
  'cairo' 
  'gdk-pixbuf2' 
  'glib2' 
  'pango' 
  'libappindicator-gtk3'
)

makedepends=(
  'cargo' 
  'nodejs' 
  'npm'
)
options=('!lto') 

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pierspad/textmerger/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  cd textmerger
  npm ci
}

build() {
  cd "$pkgname-$pkgver/textmerger"
  npm run tauri build -- --no-bundle
}

package() {
  cd "$pkgname-$pkgver/textmerger"
  install -Dm755 src-tauri/target/release/textmerger "$pkgdir/usr/bin/textmerger"
  install -Dm644 src-tauri/icons/128x128.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/textmerger.png"
  mkdir -p "$pkgdir/usr/share/applications"
  cat > "$pkgdir/usr/share/applications/textmerger.desktop" <<EOF
[Desktop Entry]
Type=Application
Name=TextMerger
Comment=Merge text files easily
Exec=textmerger
Icon=textmerger
Categories=Utility;
EOF
}
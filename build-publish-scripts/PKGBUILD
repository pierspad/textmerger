# Maintainer: pierspad <pierpaolospadafora@proton.me>
pkgname=textmerger
pkgver=2.0.0
pkgrel=3
pkgdesc="A Rust/Tauri GTK3 application for merging text files"
arch=('x86_64')
url="https://github.com/pierspad/textmerger"
license=('GPL3')

depends=(
  'webkit2gtk'
  'gtk3'
  'cairo'
  'gdk-pixbuf2'
  'glib2'
  'pango'
  'libappindicator-gtk3'
)

makedepends=('cargo' 'nodejs' 'npm')

source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/pierspad/textmerger/archive/refs/tags/v$pkgver.tar.gz"

)
sha256sums=('edfb3b72a02cb6e473f54433c1c67a27100cb0c3a3c7df51b800bae2533ce54c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/textmerger"
  echo "Installing Node dependencies..."
  npm install
  
  rm -f src-tauri/.cargo/config.toml
}

build() {
  cd "$srcdir/$pkgname-$pkgver/textmerger"
  
  echo "Building Tauri application..."
  sed -i 's/"active": true/"active": false/' src-tauri/tauri.conf.json
  
  npm run tauri build
}

package() {
  cd "$srcdir/$pkgname-$pkgver/textmerger"
  
  install -Dm755 "src-tauri/target/release/textmerger" "$pkgdir/usr/bin/textmerger"
  
  install -Dm644 "../packaging/textmerger.desktop" "$pkgdir/usr/share/applications/textmerger.desktop"
  
  install -Dm644 "src-tauri/icons/128x128.png" "$pkgdir/usr/share/pixmaps/textmerger.png"
  
  cd ..
  install -Dm644 docs/README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
